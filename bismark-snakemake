#hard-coded location of things - TO EDIT AS NEEDED
SAMPLES = glob_wildcards("raw/{S}.fastq.gz").S
REFERENCE = "/home/steve/genomes_temp/Bd21Control"
CONTEXT = ['CpG','CHG','CHH']
#software requirements
#fastqc
#trim_galore (cutadapt)
#samtools
#bismark
#bowtie1


#bash safe mode
shell.executable("/bin/bash")
shell.prefix("set -euo pipefail; ")

ruleorder: fastqc > trim > alignment > methylation_extractor > bismark2bed

rule final:
    input:
        expand("cleaned/{sample}_trimmed.fq.gz", sample=SAMPLES),
        expand("qc/{sample}_fastqc.html", sample=SAMPLES),
        expand("qc/{sample}_fastqc.zip", sample=SAMPLES),

        expand("out/{sample}/{sample}_trimmed_bismark.bam", sample=SAMPLES),
        expand("out/{sample}/{sample}_trimmed_bismark.deduplicated.bam", sample=SAMPLES),
        expand("out/{sample}/{sample}_trimmed_bismark.deduplicated.sorted.bam.bai",sample=SAMPLES),
        expand("out/{sample}/{sample}_trimmed_bismark.deduplicated.sorted.bam",sample=SAMPLES),

        expand("out/{sample}/{sample}_trimmed_bismark.nucleotide_stats.txt", sample=SAMPLES),
        expand("out/{sample}/{sample}_trimmed_bismark.deduplication_report.txt", sample=SAMPLES),
        expand("out/{sample}/{sample}_trimmed_bismark_SE_report.txt", sample=SAMPLES),
        expand("out/{sample}/{sample}_trimmed_bismark.deduplicated.sorted.M-bias.txt", sample=SAMPLES),
        expand("out/{sample}/{sample}_trimmed_bismark.deduplicated.sorted_splitting_report.txt", sample=SAMPLES),

        expand("out/{sample}/{context}_context_{sample}_trimmed_bismark.deduplicated.sorted.txt.gz", sample=SAMPLES, context=CONTEXT),

        expand("out/{sample}/{sample}_{context}.gz.bismark.cov.gz", sample=SAMPLES,context=CONTEXT),
        expand("out/{sample}/{sample}_{context}.gz", sample=SAMPLES,context=CONTEXT),

rule fastqc:
    input:
        r1="raw/{sample}.fastq.gz",
    output:
        q1="qc/{sample}_fastqc.html",
        zip="qc/{sample}_fastqc.zip",
    shell:
        """
        fastqc {input.r1} --outdir qc
        """

rule trim:
    input:
        r1="raw/{sample}.fastq.gz",
    output:
        r1="cleaned/{sample}_trimmed.fq.gz",
        l1="cleaned/{sample}.fastq.gz_trimming_report.txt",
    shell:
        """
        trim_galore {input.r1} --output_dir cleaned
        """

rule alignment:
    input:
        r1="cleaned/{sample}_trimmed.fq.gz",
    output:
        initial="out/{sample}/{sample}_trimmed_bismark.bam",
        se="out/{sample}/{sample}_trimmed_bismark_SE_report.txt",
        nuc="out/{sample}/{sample}_trimmed_bismark.nucleotide_stats.txt",
    params:
        ref=REFERENCE,
    shell:
        """
        bismark --bowtie1 --multicore 2 -n 1 -l 28 --gzip --nucleotide_coverage {params.ref} {input.r1} --output_dir out/{wildcards.sample}
        """

rule deduplicate:
    input:
        bam="out/{sample}/{sample}_trimmed_bismark.bam",
    output:
        bam="out/{sample}/{sample}_trimmed_bismark.deduplicated.bam",
        dup="out/{sample}/{sample}_trimmed_bismark.deduplication_report.txt",
    shell:
        """
        deduplicate_bismark --bam {input.bam}
        """

rule sort:
    input:
        bam="out/{sample}/{sample}_trimmed_bismark.deduplicated.bam",
    output:
        sort="out/{sample}/{sample}_trimmed_bismark.deduplicated.sorted.bam",
        bai="out/{sample}/{sample}_trimmed_bismark.deduplicated.sorted.bam.bai",
    shell:
        """
        samtools sort {input.bam} > {output.sort}
        samtools index {output.sort}
        """

rule methylation_extractor:
    input:
        bam="out/{sample}/{sample}_trimmed_bismark.deduplicated.sorted.bam",
    output:
        split="out/{sample}/{sample}_trimmed_bismark.deduplicated.sorted_splitting_report.txt",
        mbias="out/{sample}/{sample}_trimmed_bismark.deduplicated.sorted.M-bias.txt",
        cg="out/{sample}/CpG_context_{sample}_trimmed_bismark.deduplicated.sorted.txt.gz",
        chg="out/{sample}/CHG_context_{sample}_trimmed_bismark.deduplicated.sorted.txt.gz",
        chh="out/{sample}/CHH_context_{sample}_trimmed_bismark.deduplicated.sorted.txt.gz",
    params:
        ref=REFERENCE,
    shell:
        """
        bismark_methylation_extractor --gzip --multicore 4 --comprehensive --report --genome_folder {params.ref} --buffer_size 8G -s {input.bam} --output out/{wildcards.sample}
        """

rule bismark2bed:
    input:
        extract="out/{sample}/{context}_context_{sample}_trimmed_bismark.deduplicated.sorted.txt.gz",
    output:
        bed="out/{sample}/{sample}_{context}.gz",
        cgcov="out/{sample}/{sample}_{context}.gz.bismark.cov.gz",
    shell:
        """
        bismark2bedGraph --CX {input.extract} -o {wildcards.sample}_{wildcards.context} --dir out/{wildcards.sample}
        """
